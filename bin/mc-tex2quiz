#!/bin/zsh -f

BASE_URL=https://michael.cadilhac.name/private/quiz/
PICS_DIR=pics/
LATEXMK=(latexmk -quiet)
BUILD_DIR=_build/

usage () {
    cat <<EOF
usage: $1 [-h] [-b BASE_URL] [-o PICS_DIR] [-l LATEXMK] [-B BUILD_DIR] MC.tex QUIZ.csv
  Compiles each question in MC.tex to a PNG file, and creates a CSV quiz
  file for D2L.  The PNG are output in the PICS_DIR directory.

Options:
  -b BASE_URL: The base URL where PNG files will be stored.
               (default: $BASE_URL)
  -o PICS_DIR: The directory in which PNGs go.
               (default: $PICS_DIR)
  -l LATEXMK: latexmk command to use.
               (default: $LATEXMK)
  -B BUILD_DIR: The directory in which PDFs and aux files go before being converted to PNGs.
               (default: $BUILD_DIR)
  -h: Prints this help message.

Notes:
  This program relies on latexmk.  The file MC.tex MUST use the variable \qnum.
EOF
    exit 1
}

while getopts 'hb:B:o:l:' c; do
    case $c in
        h) usage $0;;
        b) BASE_URL=$OPTARG;;
        B) BUILD_DIR=$OPTARG;;
        l) eval "LATEXMK=($OPTARG)";;
        o) PICS_DIR=$OPTARG;;
    esac
done

shift $((OPTIND - 1))

(( $# == 2 )) || usage $0

MCTEX=$1
QUIZCSV=$2
[[ -e $MCTEX ]] || { echo "$MCTEX: file not found." >&2; usage $0 }

## Change if alphabet changes.
ALPHABET="ABCDEFGHIJKLMNOP"

mkdir -p $PICS_DIR || exit 1
rm -f $QUIZCSV || exit 1
touch $QUIZCSV || exit 1

i=0
tail -n1 $MCTEX | sed 's/%% CORRECT[^:]*: //' | tr -d ' ' | tr ',' '\n' | \
    while read question; do
        ## Question is of the shape num@points:answer/#choices
        case $question; in
            '') continue;;
            *@*:*/*) ;;
            *) echo "Question in footer reads $question: syntax error." >&2
               exit 2;;
        esac

        (( ++i ))
        echo "Preparing question $i"

        ar=("${(@s/:/)${question//[^0-9]/:}}")
        num=$ar[1]
        points=$ar[2]
        correct=$ar[3]
        nchoices=$ar[4]

        jobname=${${MCTEX:t}/.tex/}-$num
        pretex="\\def\\qnum{$num}"

        ## Compiling
        cmd=($LATEXMK -jobname=$jobname -pretex=$pretex -usepretex -outdir=$BUILD_DIR)
        $cmd || { echo "Compilation error while running $cmd"; exit 2 }

        ## Converting to PNG
        md5=$(md5sum $BUILD_DIR/$jobname.pdf | cut -d' ' -f1)
        if [[ ! -e $PICS_DIR/$md5.png || $BUILD_DIR/$jobname.pdf -nt $PICS_DIR/$md5.png ]]; then
            convert -density 200 $BUILD_DIR/$jobname.pdf -colorspace RGB $PICS_DIR/$md5.png ||
                { echo "Conversion error.  Aborted."; exit 3 }
        fi

        truth=(0 0 0 0 0)
        truth[$correct]=100  # in zsh, arrays start at 1

        ## Write question in CSV
        cat >> $QUIZCSV <<EOF
NewQuestion,MC,
Title,Question $i
QuestionText,<img width="100%" src="$BASE_URL/$md5.png">,HTML
Points,$points,
Difficulty,1,
EOF
        ## Write answers in CSV
        for j in {1..$nchoices}; do
            if ((correct == j)); then
                truth=100
            else
                truth=0
            fi
            echo "Option,$truth,\"Answer $ALPHABET[$j].\",HTML,\"no feedback\"" >> $QUIZCSV
        done
    done
